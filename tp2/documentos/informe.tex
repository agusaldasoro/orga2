\documentclass[a4paper]{article}
\usepackage[spanish]{babel}
\usepackage[utf8]{inputenc}
\usepackage{charter}   % tipografia
\usepackage{graphicx}
%\usepackage{makeidx}
\usepackage{paralist} %itemize inline

%\usepackage{float}
%\usepackage{amsmath, amsthm, amssymb}
%\usepackage{amsfonts}
%\usepackage{sectsty}
%\usepackage{charter}
%\usepackage{wrapfig}
%\usepackage{listings}
%\lstset{language=C}


\input{codesnippet}
\input{page.layout}
% \setcounter{secnumdepth}{2}
\usepackage{underscore}
\usepackage{caratula}
\usepackage{url}


% ******************************************************** %
%              TEMPLATE DE INFORME ORGA2 v0.1              %
% ******************************************************** %
% ******************************************************** %
%                                                          %
% ALGUNOS PAQUETES REQUERIDOS (EN UBUNTU):                 %
% ========================================
%                                                          %
% texlive-latex-base                                       %
% texlive-latex-recommended                                %
% texlive-fonts-recommended                                %
% texlive-latex-extra?                                     %
% texlive-lang-spanish (en ubuntu 13.10)                   %
% ******************************************************** %



\begin{document}


\thispagestyle{empty}
\materia{Organización del Computador II}
\submateria{Segundo Cuatrimestre de 2014}
\titulo{Trabajo Práctico II}
\subtitulo{Programaci\'on SIMD}
\integrante{Agustina Aldasoro}{86/13}{agusaldasoro@gmail.com}
\integrante{Maximiliano Rey}{37/13}{rey.maximiliano@gmail.com}
\integrante{Ignacio Tirabasso}{718/12}{ignacio.tirabasso@gmail.com}

\maketitle
\newpage

\thispagestyle{empty}
\vfill
\begin{abstract}
En el presente trabajo se describen los beneficios de la programaci\'on en Lenguaje Ensamblador bajo el modelo de programaci\'on SIMD mediante el uso de instrucciones SSE.
\end{abstract}


\thispagestyle{empty}
\vspace{3cm}
\tableofcontents
\newpage


%\normalsize
\newpage

\section{Objetivos generales}

El objetivo de este Trabajo Práctico es evaluar la eficiencia del modelo de programaci\'on SIMD mediante la implementaci\'on de diversos algoritmos en lenguaje Ensamblador utilizando instrucciones SSE. \\
\indent Las mediciones se realizan mediante pruebas emp\'iricas del c\'odigo frente a algoritmos que cumplen la misma especificaci\'on, implementados en un lenguaje de alto nivel (C). \\
\indent En este proyecto, los algoritmos a implementar se basaron en el procesamiento de im\'agenes y video, en el cual el uso del modelo SIMD es provechoso.


\section{Contexto}

\input{enunciado}


\newpage
Abordando el objetivo de este trabajo, realizamos una experimentaci\'on enfocada en reducir el tiempo de c\'omputo de un programa basado en dos sucesos que tienen la capacidad de afectarlo. \\
\indent Por un lado se encuentra la \textit{capacidad de c\'omputo}, la cual limita la cantidad de operaciones aritm\'eticas que el procesador puede paralelizar. Si el programa ejecuta un uso intensivo en operaciones aritm\'eticas, al añadirle nuevas operaciones de esta \'indole se van a necesitar m\'as ciclos de clock para ejecutarlas. \\
\indent El otro cuello de botella importante es el \textit{ancho de banda de la memoria}. Cuando el programa ejecuta una instrucci\'on que implica un acceso a memoria, se precisan m\'as ciclos de clock para que la memoria responda, en particular si el dato no se encuentra en el cache.\\
\indent Diseñamos nuestros casos de testeo con el fin de observar para cada programa cu\'al es el factor que determina el tiempo de c\'omputo.\\
\\
\indent Nuestra experimentaci\'on se centra en la cantidad de ciclos de clock que transcurren desde el inicio hasta el final de la ejecuci\'on del programa. Se adjunta con la documentaci\'on los archivos *.py utilizados para calcular la esperanza y la varianza de cada una de las mediciones.\\
\\
\textcolor{red}{Si lo vamos a rearmar, habria que explicar un poquito mas que hace el codigo, cuantas iteraciones y que descarta los maximos y minimos}\\

%\begin{figure}
%  \begin{center}
%	\includegraphics[scale=0.66]{imagenes/logouba.jpg}
%	\caption{Descripcion de la figura}
%	\label{nombreparareferenciar}
%  \end{center}
%\end{figure}


%\paragraph{\textbf{Titulo del parrafo} } Bla bla bla bla.
%Esto se muestra en la figura~\ref{nombreparareferenciar}.
%\begin{codesnippet}
%\begin{verbatim}
%
%struct Pepe {
%
%    ...
%
%};
%\end{verbatim}
%\end{codesnippet}
\newpage
\section{Implementaci\'on en Assembler}

\subsection{Filtro CropFlip}
%\begin{codesnippet}
%\begin{verbatim}
\indent Luego de pushear los cinco registros a utilizar, almacenamos: \\
\begin{codesnippet}
\begin{verbatim}
Pushear la base de la pila y los registros a utilizar.
Alinear la pila.
mov r12d, [rbp+16] ;tamx
mov r13d, [rbp+24] ;tamy
mov r14d, [rbp+32] ;offsetx
mov r15d, [rbp+40] ;offsety
Limpiamos la parte alta de estos registros haciendo un mov rXd, rXd.
\end{verbatim}
\end{codesnippet}

 \indent Utilizamos el registro \textbf{r10} como el \emph{y-actual} (fuente) y el registro \textbf{r11} como el \emph{x-actual} (fuente). Recorremos la imagen fuente desde arriba hacia abajo, de izquierda a derecha.\\
 \begin{codesnippet}
\begin{verbatim}
	mov r10,r15 ; y
	mov r11,r14 ; x
\end{verbatim}
\end{codesnippet}
 \indent Utilizamos el registro \textbf{rcx} como el \emph{y$_2$-actual} (destino) y el registro \textbf{rdx} como el \emph{x$_2$-actual} (destino). Recorremos la imagen destino de abajo hacia arriba, de izquierda a derecha. Al registro rcx debemos decrementarlo en uno porque arranca inicializado en cero. \\
 \begin{codesnippet}
\begin{verbatim}
mov rcx,r13 ; y2 = tamy
dec rcx     ; y2 = tamy-1
mov rdx,0   ; x2 = 0
\end{verbatim}
\end{codesnippet}

\indent En \textbf{r13} almacenamos el l\'imite para r10, es decir tiene que recorrer el ciclo de y hasta que alcance su l\'imite en y (offsety+tamy). \\
	  \begin{codesnippet}
\begin{verbatim}
	add r13,r15 ; r13 = offsety+tamy
\end{verbatim}
\end{codesnippet}
\indent En \textbf{rbx} almacenamos el l\'imite para r11, es decir tiene que recorrer el ciclo de x hasta que alcance su l\'imite en x (offsetx+tamx)\\
	  \begin{codesnippet}
\begin{verbatim}
mov rbx,r12  ; rbx = tamx
	add rbx,r14  ; rbx = offsetx+tamx
\end{verbatim}
\end{codesnippet}

\indent Ahora comienza el ciclo de iteraci\'on sobre la variable \emph{y}. Cada vez que se ejecuta se comprueba que el \emph{y-actual} (\textbf{r10}) sea menor que su l\'imite (\textbf{r13}). Y por cada iteraci\'on se reinician los valores de \emph{x} e \emph{x$_2$} a la primer columna que debemos trabajar (Para la imagen fuente es el offset inicial de la variable \emph{x}, para la imagen destino es el \emph{0}).
	  \begin{codesnippet}
\begin{verbatim}
.loop_y:
	cmp r10,r13     ; y < offsety+tamy
	jge .endloop_y
	mov r11,r14     ; x = offsetx
	mov rdx,0       ; x2 = 0
\end{verbatim}
\end{codesnippet}

\indent Por consiguiente, para cada valor que vaya tomando la variable, se debe ejecutar un ciclo para poder iterar sobre todas las columnas (Ciclo de x). Por cada iteraci\'on, se compara si el \emph{x-actual} (\textbf{r11}) es menor que su l\'imite (\textbf{rbx}). Si no lo es, se salta fuera del ciclo de x. \\
\indent Luego, comienza a ejecutar el c\'odigo propio del ciclo. Es necesario tener en cuenta que: \\
$\star$ En \textbf{r8} est\'a cargado el \textit{row_size} de la imagen fuente. \\
$\star$ En \textbf{r9} est\'a cargado el \textit{row_size} de la imagen de destino. \\
\indent Vamos a calcular en cada iteraci\'on la cantidad de posiciones en memoria (bytes) que se le deben sumar a la posici\'on de origen de la imagen para encontrarnos en la posici\'on actual. Esto se almacena en el registro \textbf{rax}. Primero copiamos el contenido de \textbf{r10} (\emph{y-actual}), lo multiplicamos por el largo de cada fila para as\'i posicionarnos en la fila actual y por \'ultimo le sumamos \textbf{r11} (que indica el n\'umero de fila actual) multiplicado por 4 porque cada Pixel tiene 4 bytes. De este modo, \textbf{rax} contiene el offset que debemos sumarle a la posici\'on en memoria de la imagen fuente para situarnos en la posici\'on actual.\\
\indent Como nos encontramos utilizando registros Xmm, vamos a trabajar con 4 pixels a la vez, de modo que entran en un solo registro. Para levantar de memoria 4 pixels, utilizamos la intrucci\'on \textit{movdqu}. \\
\indent Por \'ultimo, lo que tenemos que hacer es guardar esos 4 pixels levantados con el mismo orden en la posici\'on correspondiente de la imagen destino. Es decir, la misma columna \emph{x} pero sin su offset (por eso mismo se usan dos registros distintos, ya que el offset de la imagen de destino es 0) y la fila \emph{y$_2$} va a ser la diferencia entre la cantidad de filas e \emph{y}. \\
\indent An\'alogamente a lo anterior, en \textbf{rax} se guarda el offset de la imagen destino, para acceder a memoria sum\'andoselo a la posici\'on donde esta almacenada la imagen. \\
\indent Se suma 4 a las variables r11(\emph{x-actual}) y rdx(\emph{x$_2$-actual}), porque en este paso avanzamos 4 pixels. Y el jump se ejecuta siempre, ya que la comparaci\'on se produce al principio del ciclo.
	  \begin{codesnippet}
\begin{verbatim}
.loop_x:

	cmp r11,rbx      ; x < offsetx+tamx
	jge .endloop_x

	mov rax,r10
	imul rax,r8
	lea rax,[rax+r11*4]

	movdqu xmm0,[rdi+rax]

	mov rax,rcx
	imul rax,r9
	lea rax,[rax+rdx*4]

	movdqu [rsi+rax],xmm0

	add r11,4
	add rdx,4
	jmp .loop_x	
\end{verbatim}
\end{codesnippet}

\indent Una vez copiada toda una fila, debemos avanzar hacia la siguiente. De este modo: incrementamos en uno r10(\emph{y-actual}) y rcx(\emph{y$_2$-actual}). \\
\indent Se ejecuta siempre un jump hacia el comienzo del ciclo en y, porque la comparaci\'on de ver si ya copiamos todas las filas se ejecuta al principio.
	  \begin{codesnippet}
\begin{verbatim}
.endloop_x:
	inc r10
	dec rcx
	jmp .loop_y
\end{verbatim}
\end{codesnippet}

\indent Una vez terminado el ciclo de y, s\'olo resta salir de la ejecuci\'on respetando la convenci\'on C.
	  \begin{codesnippet}
\begin{verbatim}
.endloop_y:
 Alinear la pila y popear todos los registros.
 ret
\end{verbatim}
\end{codesnippet}

\newpage
\subsection{Filtro Sierpinsky}

\indent Primero hacemos unos defines que vamos a utilizar luego.
 \begin{codesnippet}
\begin{verbatim}
offset: dd 3.0, 2.0, 1.0, 0.0
dq255:  dd 255.0,255.0,255.0,255.0
\end{verbatim}
\end{codesnippet}

\indent Limpiamos los registros xmm15 y los que vamos a utilizar como \emph{x} e \emph{y}.
 \begin{codesnippet}
\begin{verbatim}
    Salvamos el tope de la pila y salvamos los registros a usar.
    xor r15,r15 ; x
    xor r14,r14 ; y
    pxor xmm15,xmm15
\end{verbatim}
\end{codesnippet}

\indent Primero recorremos la imagen por filas (\emph{y}) y luego por columnas (\emph{x}). Para ello, primero comenzamos con el \textit{ciclo de y}. En \textbf{ecx} estaba almacenada la cantidad total de filas de la imagen pasada por par\'ametro. Cuando alcanza este valor, finaliza el ciclo.
 \begin{codesnippet}
\begin{verbatim}
.loop_y:
    cmp r14d, ecx
    je .endloop_y     ; y = filas
\end{verbatim}
\end{codesnippet}

\indent Para entrar a este ciclo, \emph{x} no debe haber alcanzado su tama\~no m\'aximo, es decir se repite hasta que hayamos trabajado con todas las columnas de la fila actual. Como para este filtro son necesarios los valores de \emph{x} e \emph{y} para hacer c\'alculos del coeficiente para cada iteraci\'on, nos debemos encargar de copiarlos en dos registro xmm transformados en puntos flotantes. Como vamos a trabajar en simult\'aneo, mediante la instrucci\'on \textit{shufps} y d\'andole como par\'ametro el 0h replicamos el valor 4 veces en el registro. Hacemos lo mismo tanto para \emph{y} como para \emph{x}, salvando que en \emph{x} le debemos sumar el offset ya que los cuatro p\'ixeles con los que vamos a trabajar en simult\'aneo pertenecen a la misma fila y a columnas contiguas. 
 \begin{codesnippet}
\begin{verbatim}
.loop_x:
    cmp r15d,edx    ; x = cols
    je .endloop_x

    xor rax, rax 
    mov rax, r15           ; rax = x
    cvtsi2ss xmm1,rax      ; xmm1 = (float) x
    shufps xmm1, xmm1, 0h  ; replico 4 veces rax en xmm1
    movups xmm2,[offset]   ; 
    addps xmm1, xmm2       ; le sumo el offset de x en los pixels (3,2,1,0)
    mov rax, r14           ; rax = y
    cvtsi2ss xmm2, rax     ; xmm2 = (float) y
    shufps xmm2, xmm2, 0h  ; replico 4 veces rax en xmm2
\end{verbatim}
\end{codesnippet}

\indent Ahora, debemos multiplicar los valores de \emph{x} e \emph{y} por 255 y luego divirlos por la cantidad de columnas y de filas respectivamente. Para hacer esto, mantenemos el empaquetamiento de cuatro valores por registro xmm. Luego de este paso, en \textbf{xmm1} va a quedar el valor del \emph{x} actual, seguido de los valores de los tres siguientes, y en \textbf{xmm2} el valor del \emph{y} actual replicado 4 veces.
 \begin{codesnippet}
\begin{verbatim}
    movups xmm0,[dq255]
    mulps xmm1,xmm0        ; xmm1 = x*255
    mulps xmm2,xmm0        ; xmm2 = y*255
    
    cvtsi2ss xmm3, rdx     ; xmm3 = cols  
    shufps xmm3, xmm3, 0h  ; replico 4 veces cols en xmm3

    cvtsi2ss xmm4, rcx     ; xmm4 = filas
    shufps xmm4, xmm4, 0h  ; replico 4 veces filas en xmm4

    divps xmm1, xmm3       ; xmm1 = x*255/cols
    divps xmm2, xmm4       ; xmm2 = y*255/filas
\end{verbatim}
\end{codesnippet}
\newpage

\indent Lo que resta para calcular el coeficiente es hacer el xor y luego volver a dividir por 255. Para ejecutar el \textit{pxor} es necesario volver a tranformar los cuatro valores por registro en int, lo hacemos truncando. Luego se vuelve a transformar en puntos flotante de precisi\'on simple y as\'i dividirlos por 255.
 \begin{codesnippet}
\begin{verbatim}
    cvttps2dq xmm1,xmm1 ; convierte a int truncando
    cvttps2dq xmm2,xmm2

    pxor xmm1,xmm2      ; Hace el xor entre los 4 Is y los 4 Js empaquetados

    movups xmm0,[dq255]
    cvtdq2ps xmm1,xmm1  ; se convierte a float otra vez
    divps xmm1,xmm0     ; se divide por 255.0 
\end{verbatim}
\end{codesnippet}

\indent Como resultado, en \textbf{xmm1} tenemos los cuatro coeficientes que corresponden a los próximos cuatro p\'ixeles que vamos a procesar. Hacemos una copia de los coeficientes al registro \textbf{xmm15}, porque el registro \textbf{xmm1} lo vamos a utilizar para guardar las lecturas en memoria. En \textbf{rax} hacemos el c\'alculo del offset en la matriz imagen que corresponde a los \emph{x} e \emph{y} actuales. Leemos los valores de \emph{x} e \emph{y} actuales, multiplicamos al \emph{y} por el \textit{row_size} de la imagen fuente y al valor de \emph{x} lo multiplicamos por cuatro porque por cada p\'ixel son cuatro bytes. En \textbf{rdi} se encuentra el puntero al origen de la imagen, entonces para hacer la lectura simplemente sumamos rax con rdi y lo guardamos en \textbf{xmm0}, haciendo una copia en \textbf{xmm1}.
 \begin{codesnippet}
\begin{verbatim}
    movups xmm15,xmm1  
    ; float s = (i xor j) / 255.0;

    xor rax,rax
    mov r12d,r14d ; r12 = y
    mov r13d,r15d ; r13 = x
    imul r12d,r9d ; r12 = y * row_size_src
    imul r13d,4   ; r13 = x * 4
    mov eax,r12d  ; 
    add eax,r13d  ; rax = y*row_size_src + x*4 
    
    movdqu xmm0,[rdi+rax] ; Lectura de 16 Bytes del source
    movdqu xmm1,xmm0      ; Se hace una copia
\end{verbatim}
\end{codesnippet}

\indent Una vez que ya tenemos copiado en dos registros los p\'ixeles con los que vamos a trabajar, necesitamos convertirlos en puntos flotantes. Para ello es necesario primero expandirlos y por este motivo se desempaquetan mediante las instrucciones \textit{punpcklbw} y \textit{punpcklwd} con el registro \textbf{xmm2} que fue limpiado. De este modo es posible transformarlos en puntos flotantes mediante la instrucci\'on \textit{cvtdq2ps}, ya que tenemos un p\'ixel por registro y esto es 4 n\'umeros en punto flotante (4 Bytes).
 \begin{codesnippet}
\begin{verbatim}
    pxor xmm2,xmm2

    punpcklbw xmm0,xmm2 ; parte baja 
    movdqu xmm6,xmm0    ; copia de la parte baja
    punpcklwd xmm0,xmm2 ; baja de la baja 0 
    punpckhbw xmm6,xmm2 ; alta de la baja 1

    punpckhbw xmm1,xmm2 ; parte alta           
    movdqu xmm7,xmm1    ; copia de la parte alta
    punpckhwd xmm1,xmm2 ; alta de alta    3
    punpcklwd xmm7,xmm2 ; baja de la alta 2

    cvtdq2ps xmm0,xmm0
    cvtdq2ps xmm6,xmm6
    cvtdq2ps xmm1,xmm1
    cvtdq2ps xmm7,xmm7
\end{verbatim}
\end{codesnippet}

\indent En \textbf{xmm15} hab\'iamos copiado los coeficientes, ahora que ya tenemos los cuatro p\'ixeles con sus valores en puntos flotantes s\'olo resta multiplicarlos. Primero hacemos una copia de \textbf{xmm15} en \textbf{xmm14} porque lo vamos a pisar, ya que en cada caso necesitamos replicar cuatro veces los cuatro valores de coeficientes almacenados. De este modo, los tres canales de cada p\'ixel quedan multiplicados con su mismo coeficiente. Mediante \textit{shufps} replicamos el valor que necesitamos, tal cual figura comentado en binario.
 \begin{codesnippet}
\begin{verbatim}
    movdqu xmm14,xmm15
    shufps xmm15,xmm15, 0x00 ; 00 00 00 00
    mulps xmm1,xmm15

    movdqu xmm15,xmm14
    shufps xmm15,xmm15, 0x55 ; 01 01 01 01 
    mulps xmm7,xmm15       
    
    movdqu xmm15,xmm14
    shufps xmm15,xmm15, 0xAA ; 10 10 10 10
    mulps xmm6,xmm15
    
    movdqu xmm15,xmm14
    shufps xmm15,xmm15, 0xFF ; 11 11 11 11
    mulps xmm0,xmm15
\end{verbatim}
\end{codesnippet}

\indent Una vez ya calculados todos los valores finales, debemos transformarlos en enteros y empaquetarlos al tama\~no de char como era originalmente para poder asign\'arselos a la imagen destino. Mediante la instrucci\'on \textit{cvttps2dq} se convierten los cuatro valores de cada p\'ixel, que estaban en puntos flotantes de precisi\'on simple a enteros empaquetados. Luego, mediante la instrucci\'on \textit{packusdw} empaquetamos de a dos registros, para as\'i modificar le tama\~no de ints a shorts. Por \'ultimo, unimos los cuatro p\'ixeles en un registro con la instrucci\'on packusdw que los transforma de shorts a char. Todo esto lo realizamos mediante unsigned saturation para eliminar casos de exceso que puedan hacer que la fotograf\'ia final no quede como lo deseado. Debemos volver a calcular \textbf{rax} para la imagen de destino, ya que no necesariamente tengan el mismo \textit{row_size}, el c\'odigo es el mismo que el \'utilizado anteriormente, s\'olo que en \textbf{r8d} se encuentra el tama\~no de row_size_destino. En rsi se encuentra el puntero a la imagen de destino, por este motivo se suman ambos registros y se asigna a esa posici\'on de memoria lo calculado que se encuentra en el registro \textbf{xmm0}. Avanzamos 4 en \emph{x}, porque le\'imos cuatro p\'ixeles y saltamos al comienzo del ciclo en \emph{x} otra vez.
 \begin{codesnippet}
\begin{verbatim}
    cvttps2dq xmm1,xmm1
    cvttps2dq xmm0,xmm0
    cvttps2dq xmm6,xmm6
    cvttps2dq xmm7,xmm7

    packusdw xmm0,xmm6
    packusdw xmm7,xmm1

    packuswb xmm0,xmm7

    xor rax,rax
    mov r12d,r14d ; r12 = y
    mov r13d,r15d ; r13 = x
    imul r12d,r8d ; y * row_size_dest
    imul r13d,4   ; x * 4
    mov eax,r12d  ; 
    add eax,r13d  ; rax = y*row_size_dest + x*4 

    movdqa [rsi+rax],xmm0

    add r15,4 ; avanzo 4 sobre x
    jmp .loop_x
\end{verbatim}
\end{codesnippet}

 \indent Para culminar el ciclo de \emph{x}, reiniciamos a cero el valor de \emph{x} y aumentamos en uno el valor de \emph{y}, volviendo al comienzo del ciclo de \emph{y}.
 \begin{codesnippet}
\begin{verbatim}
.endloop_x:
    inc r14     ; y++
    xor r15,r15 ; reinicio x
    jmp .loop_y
\end{verbatim}
\end{codesnippet}

\indent Cuando termin\'o el ciclo de \emph{y}, termin\'o la ejecuci\'on del filtro. Por lo tanto, popeamos los registros usados y retornamos.
 \begin{codesnippet}
\begin{verbatim}
.endloop_y:
    Hacemos pop de los registros salvados
    ret
\end{verbatim}
\end{codesnippet}

\newpage
\subsection{Filtro Bandas}

\indent Algunos defines que luego vamos a necesitar
\begin{codesnippet}
\begin{verbatim}
color1 dw 64,64,64,64,64,64,64,64
color3 db 128,128,128,255
color4 db 192,192,192,255
color5 db 255,255,255,255

all_64w dw -64,-64,-64,-64,-64,-64,-64,-64

color1_bound dw 96,96,96,96,96,96,96,96
color2_bound dw 288,288,288,288,288,288,288,288
color3_bound dw 480,480,480,480,480,480,480,480
color4_bound dw 672,672,672,672,672,672,672,672

rgb_only_mask dd 0x00FFFFFF,0x00FFFFFF,0x00FFFFFF,0x00FFFFFF

all_1_mask    dd 0xFFFFFFFF,0xFFFFFFFF,0xFFFFFFFF,0xFFFFFFFF
all_0_mask    dd 0x00000000,0x00000000,0x00000000,0x00000000

magic_shuffle db 8,8,8,8,10,10,10,10,0,0,0,0,2,2,2,2
\end{verbatim}
\end{codesnippet}
 
\indent Salvamos los registros a utilizar, para respetar la convenci\'on C. Luego limpiamos los registros que vamos a usar como \emph{x} e \emph{y} y el registro xmm15.
\begin{codesnippet}
\begin{verbatim}
    Pusheamos los registros que vamos a utilizar
    xor r15,r15 ; x
    xor r14,r14 ; y
    pxor xmm15,xmm15
\end{verbatim}
\end{codesnippet}

\indent Vamos a recorrer la imagen primero por filas y luego por columnas, por este motivo comenzamos con el ciclo de \emph{y}, el cual termina cuando el \emph{y} actual (\textbf{r14}) alcance su valor m\'aximo de filas que est\'a almacenado en \textbf{ecx}.
\begin{codesnippet}
\begin{verbatim}
.loop_y:
    cmp r14d,ecx
    je .endloop_y  ; y = filas
\end{verbatim}
\end{codesnippet}

\indent Comienza el ciclo de \emph{x}, el cual termina cuando \textbf{r15} alcanza el valor m\'aximo de columnas el cual est\'a almacenado en \textbf{edx}. Luego, en \textbf{rax} vamos a calcular el offset dentro de la matriz imagen sumando el valor de \emph{y} actual multiplicado por el row_size de destino con el valor de \emph{x} actual multiplicado por cuatro porque por cada p\'ixel hay cuatro bytes.
\begin{codesnippet}
\begin{verbatim}
.loop_x:
    cmp r15d,edx
    je .endloop_x  ; x = cols

    ; armo el offset
    xor rax,rax
    mov r12d,r14d ; r12 = y
    mov r13d,r15d ; r13 = x
    imul r12d,r9d ; y * row_size_src
    imul r13d,4   ; x * 4 
    mov eax,r12d  ; 
    add eax,r13d  ; rax = y*row_size_src + x*4 
\end{verbatim}
\end{codesnippet}

\indent Luego de haber calculado el offset actual, leemos de memoria 16 Bytes y los almacenamos en \textbf{xmm1}, copi\'andolos tambi\'en en \textbf{xmm2}. Para evitar inconvenientes, ponemos en cero al canal alpha mediante el \textit{pand} y la etiqueta definida antes \textit{rgb_only_mask}. De la lectura que hicimos, guardamos en \textbf{xmm1} la parte baja y en \textbf{xmm2} la parte alta desempaquetando.

\textcolor{red}{No entiendo la parte del codigo que sigue... que hace? porque lo hace?}
\begin{codesnippet}
\begin{verbatim}
    movdqu xmm1,[rdi+rax] ; agarro 16 bytes del source
    movdqu xmm2,xmm1

    movdqu xmm15,[rgb_only_mask]
    pand xmm1,xmm15
    pand xmm2,xmm15

    pxor xmm7,xmm7
    punpcklbw xmm1,xmm7 ; baja 0
    punpckhbw xmm2,xmm7 ; alta 8

    pxor xmm7,xmm7
    pxor xmm8,xmm8

    phaddw xmm1,xmm7
    phaddw xmm7,xmm1 
    
    movdqu xmm1,xmm7

    phaddw xmm2,xmm8
    phaddw xmm2,xmm8

    paddw xmm1,xmm2
\end{verbatim}
\end{codesnippet}

\indent Copiamos en los registros \textbf{xmm14}, \textbf{xmm13} y \textbf{xmm12} las etiquetas predefinidas. Luego en \textbf{xmm15} vamos a ir copiando la etiqueta que necesitemos.

\textcolor{red}{Despues no se que hace... jeje}
\begin{codesnippet}
\begin{verbatim}
    movdqu xmm14,[all_64w]
    movdqu xmm13,[all_1_mask]
    movdqu xmm12,[color1]

    movdqu xmm15,[color1_bound]
    movdqu xmm0,xmm1 
    pcmpgtw xmm15,xmm1
    pandn xmm15,xmm13
    pand xmm15,xmm12

    movdqu xmm1,xmm15

    movdqu xmm2,xmm0
    movdqu xmm15,[color2_bound]
    pcmpgtw xmm15,xmm2
    pandn xmm15,xmm13
    pand xmm15,xmm12

    movdqu xmm2,xmm15

    movdqu xmm3,xmm0
    movdqu xmm15,[color3_bound]
    pcmpgtw xmm15,xmm3
    pandn xmm15,xmm13
    pand xmm15,xmm12

    movdqu xmm3,xmm15

    movdqu xmm4,xmm0
    movdqu xmm15,[color4_bound]
    pcmpgtw xmm15,xmm4
    pandn xmm15,xmm13
    pand xmm15,xmm12

    movdqu xmm4,xmm15
\end{verbatim}
\end{codesnippet}

\indent \textcolor{red}{Completar}
\begin{codesnippet}
\begin{verbatim}
    paddusb xmm1,xmm2
    paddusb xmm1,xmm3
    paddusb xmm1,xmm4

    ; reordeno los bytes a como estaba original
    movdqu xmm15,[magic_shuffle] 
    pshufb xmm1,xmm15
    
    ; armo el offset
    xor rax,rax
    mov r12d,r14d ; r12 = y
    mov r13d,r15d ; r13 = x
    imul r12d,r8d ; y * row_size_dest
    imul r13d,4   ; x * 4 (esto puede ser un shift left)
    mov eax,r12d  ; 
    add eax,r13d  ; rax = y*row_size_dest + x*4 

    movdqu [rsi+rax],xmm1

    add r15,4 ; avanzo 4 sobre x
    jmp .loop_x
\end{verbatim}
\end{codesnippet}

\indent \textcolor{red}{Completar}
\begin{codesnippet}
\begin{verbatim}
.endloop_x:
    inc r14
    xor r15,r15 ; reinicio x
    jmp .loop_y
\end{verbatim}
\end{codesnippet}

\indent \textcolor{red}{Completar}
\begin{codesnippet}
\begin{verbatim}
.endloop_y:

	pop r15
	pop r14
	pop r13
	pop rbp
    ret
\end{verbatim}
\end{codesnippet}




\newpage
\subsection{Filtro Motion Blur}
\indent Primero hacemos algunos define que vamos a necesitar luego:
 \begin{codesnippet}
\begin{verbatim}
cerocomados  dd  0.2 , 0.2 , 0.2 , 0.2
rgb_only     dd  0x00FFFFFF, 0x00FFFFFF, 0x00FFFFFF, 0x00FFFFFF
\end{verbatim}
\end{codesnippet}

 \begin{codesnippet}
\begin{verbatim}
Salvamos la base de la pila y los registros a utilizar. Alineamos la pila.
Limpiamos con xor o pxor los registros a utilizar: r10, r11, r15, r13 y xmm13.

; r10 = i
; r11 = j
mov r15d,edx ; r15 = cols
	mov r14d,ecx ; r14 = filas
	sub r15,2 	 ; r15 = cols -2 
	sub r14,2 	 ; r14 = filas - 2 
\end{verbatim}
\end{codesnippet}

\indent El registro \textbf{xmm13} es limpiado para luego utilizarlo a la hora de asignarle el borde negro. Se les resta 2 a las filas y a las columnas porque los \'ultimos dos p\'ixeles del borde van de negro.\\
\\
\indent Comienza el ciclo de y, es decir, primero recorremos por filas y por cada fila recorremos por columnas. Se recorren todas las filas y cada vez que se entra en el \textit{loop_x} se inicializa la fila en cero.
 \begin{codesnippet}
\begin{verbatim}
.loop_y:
    cmp r10d,ecx 	; i < filas 
    jge .endloop_y
	
    mov r11,0 		; j = 0
\end{verbatim}
\end{codesnippet}

\indent El \textit{loop_x} se recorre hasta que j alcanza el valor de la \'ultima columna. En el registro \textbf{rax} vamos a asignarle el n\'umero de orden del byte a tratar, es decir la fila actual multiplicada por la cantidad de columnas sumado a la columna actual multiplicada por cuatro porque es lo que ocupa un p\'ixel. 
Antes de asignarle el nuevo valor a la imagen, debemos comprobar si el p\'ixel a tratar es de borde en cuyo caso queda negro (.cero) o si no lo es (.nocero). Todos los p\'ixeles que son bordes son los que est\'an ubicados en las primeras dos filas o columnas y los que est\'an ubicados en las \'ultimas dos filas o columnas.
 \begin{codesnippet}
\begin{verbatim}
.loop_x:
    cmp r11d,edx ; j < cols 
    jge .endloop_x

    mov rax,r10  ; rax = i
    imul eax,r8d ; rax = i * row_size 
    lea rax,[rax+r11*4] ; rax = i * row_size + j * 4

    cmp r11,2   
    jl .cero    ; j < 2
    cmp r10,2 
    jl .cero    ; i < 2
    cmp r10,r14
    jge .cero   ; i >= cols -2
    cmp r11,r15
    jge .cero   ; j >= filas -2

    jmp .nocero
\end{verbatim}
\end{codesnippet}

\newpage
\indent Si el p\'ixel a tratar debe quedar en negro (cero) lo que debemos hacer es asignarle ceros mediante el registro \textbf{xmm13}:
 \begin{codesnippet}
\begin{verbatim}
.cero:
    movq [rsi+rax],xmm13        
    add r11,2
    jmp .loop_x
\end{verbatim}
\end{codesnippet}

A continuaci\'on comienza la rutina a llevar a cabo en caso de que el p\'ixel a tratar no sea borde. Se copia en \textbf{rbx} el puntero actual con el que vamos a trabajar. Recordar que en \textbf{rax} ten\'iamos el offset en la imagen y en \textbf{rdi} el puntero al comienzo de la misma. Vamos a ejecutar 4 p\'ixeles por vez (porque son los que entran en un registro xmm) y para ello necesitamos hacer cinco lecturas a memoria y as\'i tener para cada p\'ixel sus cuatro vecinos que van a influir en su valor final. En \textbf{xmm14} copiamos el contenido de \textit{rgb_only} el cual nos permitir\'a s\'olo trabajar con los canales RGB de cada p\'ixel m\'as adelante.
 \begin{codesnippet}
\begin{verbatim}
.nocero:
    
    lea rbx,[rdi+rax]
    movdqu xmm1,[rbx]        ; (i,j)
    movdqu xmm2,[rbx+r8*1+4] ; (i+1,j+1)
    movdqu xmm3,[rbx+r8*2+8] ; (i+2,j+2)
    mov r12,rbx
    sub r12,r8
    movdqu xmm4,[r12-4]      ; (i-1,j-1)
    sub r12,r8
    movdqu xmm5,[r12-8]      ; (i-2,j-1)

    movdqu xmm14,[rgb_only]
\end{verbatim}
\end{codesnippet}

\indent Para continuar necesitamos desempaquetar los valores para poder hacer las cuentas, ya que necesitamos los valores de los p\'ixeles agrupados en n\'umeros de mayor tama\~no para el cual contemos con sus instrucciones necesarias y tambi\'en un orden de precisi\'on mayor. Para ello, los expandimos con ceros que van a provenir de haber limpiado el registro \textbf{xmm0}.

 \begin{codesnippet}
\begin{verbatim}
    pxor xmm0,xmm0
    movdqu xmm6,xmm1      ; xmm6 = (i,j) || (i,j+1) || (i,j+2) || (i,j+3)
    punpcklbw xmm1,xmm0   ; xmm1 = (i,j) || (i,j+1)
    punpckhbw xmm6,xmm0   ; xmm6 = (i,j+2) || (i,j+3)

    movdqu xmm7,xmm2      ; xmm7 = (i+1,j+1) || (i+1,j+2) || (i+1,j+3) || (i+1,j+4)
    punpcklbw xmm2,xmm0   ; xmm2 = (i+1,j+1) || (i+1,j+2)
    punpckhbw xmm7,xmm0   ; xmm7 = (i+1,j+3) || (i+1,j+4)

    movdqu xmm8,xmm3      ; xmm8 = (i+2,j+2) || (i+2,j+3) || (i+2,j+4) || (i+2,j+5)
    punpcklbw xmm3,xmm0   ; xmm3 = (i+2,j+2) || (i+2,j+3)
    punpckhbw xmm8,xmm0   ; xmm8 = (i+2,j+4) || (i+2,j+5)

    movdqu xmm9,xmm4      ; xmm9 = (i-1,j-1) || (i-1,j) || (i-1,j+1) || (i-1,j+2)
    punpcklbw xmm4,xmm0   ; xmm4 = (i-1,j-1) || (i-1,j)
    punpckhbw xmm9,xmm0   ; xmm9 = (i-1,j+1) || (i-1,j+2)

    movdqu xmm10,xmm5     ; xmm10 = (i-2,j-1) || (i-2,j) || (i-2,j+1) || (i-2,j+2)
    punpcklbw xmm5,xmm0   ; xmm5 = (i-2,j-1) || (i-2,j)
    punpckhbw xmm10,xmm0  ; xmm10 = (i-2,j+1) || (i-2,j+2)
\end{verbatim}
\end{codesnippet}
\newpage

\indent Por cada registro tenemos dos p\'ixeles, de este modo ya podemos realizar la suma entre los cinco p\'ixeles vecinos que luego de dividirlo por la constante van a ser asignados al p\'ixel central de estos cinco. De este modo, contamos con la instrucci\'on \textit{paddw} la cual suma empaquetadamente de a Word (2 bytes). Utilizamos los registros \textbf{xmm1} y \textbf{xmm6} como acumuladores de la suma.
 \begin{codesnippet}
\begin{verbatim}

    paddw xmm1,xmm2     ; xmm1 = (i,j)+(i+1,j+1)  ||  (i,j+1)+(i+1,j+2)
    paddw xmm1,xmm3     ; xmm1 = (i,j)+(i+1,j+1)+(i+2,j+2)  ||  (i,j+1)+(i+1,j+2)+(i+2,j+3)
    paddw xmm1,xmm4     ; xmm1 = (i,j)+(i+1,j+1)+(i+2,j+2)+(i-1,j-1)  ||
                                 (i,j+1)+(i+1,j+2)+(i+2,j+3)+(i-1,j)
    paddw xmm1,xmm5     ; xmm1 = (i,j)+(i+1,j+1)+(i+2,j+2)+(i-1,j-1)+(i-2,j-1)  ||
                                 (i,j+1)+(i+1,j+2)+(i+2,j+3)+(i-1,j)+(i-2,j)

    paddw xmm6,xmm7     ; xmm6 = (i,j+2)+(i+1,j+3)  ||  (i,j+3)+(i+1,j+4)
    paddw xmm6,xmm8     ; xmm6 = (i,j+2)+(i+1,j+3)+(i+2,j+4)  ||  (i,j+3)+(i+1,j+4)+(i+2,j+5)
    paddw xmm6,xmm9     ; xmm6 = (i,j+2)+(i+1,j+3)+(i+2,j+4)+(i-1,j+1)  ||
                                 (i,j+3)+(i+1,j+4)+(i+2,j+5)+(i-1,j+2)
    paddw xmm6,xmm10    ; xmm6 = (i,j+2)+(i+1,j+3)+(i+2,j+4)+(i-1,j+1)+(i-2,j+1)  ||
                                 (i,j+3)+(i+1,j+4)+(i+2,j+5)+(i-1,j+2)+(i-2,j+2)
\end{verbatim}
\end{codesnippet}

\indent Para poder multiplicarlos por una constante debemos transformar los valores en puntos flotantes, para ello primero debemos desempaquetarlos ampliandolos con cero (adquiriendo as\'i un tama\~no de Doubleword) y luego mediante la instrucci\'on \textit{cvtdq2ps} convertirlos de enteros con signo a puntos flotantes de precisi\'on simple sin perder el empaquetamiento que nos permite trabajar con los tres valores del p\'ixel a la vez. (En realidad son cuatro, pero un canal no lo usamos).
 \begin{codesnippet}
\begin{verbatim}
    movdqu xmm2,xmm1        ; xmm2 = (i,j)+(i+1,j+1)+(i+2,j+2)+(i-1,j-1)+(i-2,j-1)   ||
                                     (i,j+1)+(i+1,j+2)+(i+2,j+3)+(i-1,j)+(i-2,j)
    movdqu xmm7,xmm6        ; xmm7 = (i,j+2)+(i+1,j+3)+(i+2,j+4)+(i-1,j+1)+(i-2,j+1) ||
                                     (i,j+3)+(i+1,j+4)+(i+2,j+5)+(i-1,j+2)+(i-2,j+2)

    punpcklwd xmm1,xmm0     ; xmm1 = (i,j)+(i+1,j+1)+(i+2,j+2)+(i-1,j-1)+(i-2,j-1)
    punpckhwd xmm2,xmm0     ; xmm2 = (i,j+1)+(i+1,j+2)+(i+2,j+3)+(i-1,j)+(i-2,j)

    punpcklwd xmm6,xmm0     ; xmm6 = (i,j+2)+(i+1,j+3)+(i+2,j+4)+(i-1,j+1)+(i-2,j+1)
    punpckhwd xmm7,xmm0     ; xmm7 = (i,j+3)+(i+1,j+4)+(i+2,j+5)+(i-1,j+2)+(i-2,j+2)

    cvtdq2ps xmm1,xmm1  
    cvtdq2ps xmm2,xmm2  
    cvtdq2ps xmm6,xmm6  
    cvtdq2ps xmm7,xmm7
\end{verbatim}
\end{codesnippet}

\indent Ahora que los valores ya est\'an trasnformados en puntos flotantes, podemos hacer la multiplicaci\'on (tambi\'en empaquetada). De este modo, calculamos todo un p\'ixel por cada registro. Una vez hechas las multiplicaciones, volvemos a convertir los valores en enteros mediante la instrucci\'on \textit{cvtps2dq}. Como hab\'iamos definido antes, \textit{cerocomados} guarda el valor 0.2 cuatro veces.

 \begin{codesnippet}
\begin{verbatim}
    movdqu xmm15,[cerocomados] 

    mulps xmm1,xmm15
    mulps xmm2,xmm15
    mulps xmm6,xmm15
    mulps xmm7,xmm15

    cvtps2dq xmm1,xmm1
    cvtps2dq xmm2,xmm2
    cvtps2dq xmm6,xmm6
    cvtps2dq xmm7,xmm7
\end{verbatim}
\end{codesnippet}
\newpage

\indent Ahora resta volver a empaquetar para que queden los cuatro p\'ixeles, que est\'abamos trabajando en simultaneo, en un s\'olo registro. Primero juntamos el valor final de \textbf{xmm1} $(i,j)$ con el de \textbf{xmm2} $(i,j+1)$ y el de \textbf{xmm6} $(i,j+2)$ con el del \textbf{xmm7} $(i,j+3)$ y por \'ultimo unimos todos en un mismo registro (\textbf{xxm1}). Guardamos el contenido de xmm1 en memoria, a la direcci\'on calculada previamente en \textbf{rax}, sumada al origen del puntero destino (\textbf{rsi}). Sumamos cuatro al contador de \textbf{r11} (columnas) porque por cada lectura en memoria, trabajamos con 4 p\'ixeles, y  luego volvemos al ciclo de x.
 \begin{codesnippet}
\begin{verbatim}
    packusdw xmm1,xmm2
    packusdw xmm6,xmm7

    packuswb xmm1,xmm6

    movdqu [rsi+rax],xmm1   

    add r11,4
    jmp .loop_x
\end{verbatim}
\end{codesnippet}

\indent Al terminar el ciclo de x, lo que hacemos en incrementar en uno \textbf{r10}, el cual mide la fila actual. Es decir, avanzamos de fila y saltamos al ciclo de y.
\begin{codesnippet}
\begin{verbatim}
.endloop_x:
    inc r10
    jmp .loop_y
\end{verbatim}
\end{codesnippet}

\indent Al terminar el ciclo de y, queda finalizada la ejecuci\'on del filtro. Por lo tanto, solo queda restaurar los registros y retornar.
\begin{codesnippet}
\begin{verbatim}
.endloop_y:
    Hacemos pop de los registros salvados
    ret
\end{verbatim}
\end{codesnippet}
\newpage
\section{Enunciado y soluci\'on} 

%\input{enunciado}

\section{Conclusiones y trabajo futuro}
\indent\emph{ \textcolor{red}{Si bien un lenguaje ensamblador solo es utilizable en determinada gama de procesadores (ESTO HAY QUE SACARLO DIJO MATIAS)}}, los resultados de nuestras mediciones han demostrado que la implementación de los filtros en este lenguaje han logrado una performance que el gcc no logro conseguir. \\
\indent Dada la popularidad de la arquitectura de 64 bits de intel, se puede concluir que la implementación del SIMD en assembler tiene un precio razonable frente a la perfomance alcanzada.\\


\end{document}

